apply from: "../../GradleCommon/common.gradle"

import org.apache.tools.ant.filters.ReplaceTokens

evaluationDependsOn(":AuraBan-api")

configurations {
    slf4j {
        transitive = false
    }
}

sourceSets {
    main {
        java {
            srcDir project(":AuraBan-api").sourceSets.main.java.srcDirs
        }
        resources {
            srcDir project(":AuraBan-api").sourceSets.main.resources.srcDirs
        }
    }
}

repositories {
    maven {
        name "mikroskeem"
        url "https://repo.wut.ee/repository/mikroskeem-repo"
    }
    maven {
        name "sponge"
        url "https://repo.spongepowered.org/maven"
    }
}

dependencies {
    compile project(":AuraBan-api")

    // Normal dependencies
    compile "com.github.ben-manes.caffeine:caffeine:${caffeine_version}"
    compile "org.spongepowered:configurate-hocon:${configurate_version}"
    compile "com.h2database:h2:${h2_version}"
    compile "com.zaxxer:HikariCP:${hikariCP_version}"

    // Shadow dependencies
    shadow "eu.mikroskeem:picomaven:${picoMaven_version}"

    // Test dependencies
    testCompile "ch.vorburger.mariaDB4j:mariaDB4j:2.4.0"
    testRuntime "org.mariadb.jdbc:mariadb-java-client:2.5.1"
    testRuntime "org.slf4j:slf4j-nop:${slf4j_version}"

    // SLF4J files
    slf4j "org.slf4j:slf4j-api:${slf4j_version}"
}

processSources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "slf4jVersion", slf4j_version
    inputs.property "slf4jPluginVersion", slf4jPlugin_version

    filter (ReplaceTokens, tokens: [
        "slf4jVersion": slf4j_version,
        "slf4jPluginVersion": slf4jPlugin_version
    ])
}

shadowJar {
    relocate("eu.mikroskeem", "${rootProject.group}.shadow.eu.mikroskeem")
    relocate("licenses", "${rootProject.group}.shadow.licenses")
    relocate("org.apache", "${rootProject.group}.shadow.org.apache")
    relocate("org.codehaus", "${rootProject.group}.shadow.org.codehaus")

    exclude "module-info.class"

    from(project.configurations.slf4j.files) {
        into "org/slf4j"
        rename '(.*).jar', '$1.zip'
    }
}
