apply from: "../../GradleCommon/common.gradle"

import org.apache.tools.ant.filters.ReplaceTokens

evaluationDependsOn(":AuraBan-common")

sourceSets {
    main {
        java {
            srcDir project(":AuraBan-common").sourceSets.main.java.srcDirs
        }
        resources {
            srcDir project(":AuraBan-common").sourceSets.main.resources.srcDirs
        }
    }
}

repositories {
    maven {
        name "mikroskeem"
        url "https://repo.wut.ee/repository/mikroskeem-repo"
    }
}

dependencies {
    api project(":AuraBan-common")

    shadow "eu.mikroskeem:picomaven:${project(":AuraBan-common").picoMaven_version}"
}

processSources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "slf4jVersion", project(":AuraBan-common").slf4j_version
    inputs.property "slf4jPluginVersion", project(":AuraBan-common").slf4jPlugin_version

    filter (ReplaceTokens, tokens: [
        "slf4jVersion": project(":AuraBan-common").slf4j_version,
        "slf4jPluginVersion": project(":AuraBan-common").slf4jPlugin_version
    ])
}

shadowJar {
    relocate("eu.mikroskeem", "${rootProject.group}.shadow.eu.mikroskeem")
    relocate("licenses", "${rootProject.group}.shadow.licenses")
    relocate("org.apache", "${rootProject.group}.shadow.org.apache")
    relocate("org.codehaus", "${rootProject.group}.shadow.org.codehaus")

    exclude "module-info.class"
}
