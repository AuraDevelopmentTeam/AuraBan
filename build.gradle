apply from: "GradleCommon/common.gradle"

import org.apache.tools.ant.filters.ReplaceTokens

evaluationDependsOn(":AuraBan-api")
evaluationDependsOn(":AuraBan-bungeecord")
evaluationDependsOn(":AuraBan-velocity")
evaluationDependsOn(":AuraBan-nukkit")
evaluationDependsOn(":AuraBan-spigot")
evaluationDependsOn(":AuraBan-sponge")

configurations {
    slf4j {
        transitive = false
    }
}

sourceSets {
    main {
        java {
            srcDir project(":AuraBan-api").sourceSets.main.java.srcDirs
            srcDir project(":AuraBan-bungeecord").sourceSets.main.java.srcDirs
            srcDir project(":AuraBan-velocity").sourceSets.main.java.srcDirs
            srcDir project(":AuraBan-nukkit").sourceSets.main.java.srcDirs
            srcDir project(":AuraBan-spigot").sourceSets.main.java.srcDirs
            srcDir project(":AuraBan-sponge").sourceSets.main.java.srcDirs
        }
        resources {
            srcDir project(":AuraBan-api").sourceSets.main.resources.srcDirs
            srcDir project(":AuraBan-bungeecord").sourceSets.main.resources.srcDirs
            srcDir project(":AuraBan-velocity").sourceSets.main.resources.srcDirs
            srcDir project(":AuraBan-nukkit").sourceSets.main.resources.srcDirs
            srcDir project(":AuraBan-spigot").sourceSets.main.resources.srcDirs
            srcDir project(":AuraBan-sponge").sourceSets.main.resources.srcDirs
        }
    }
}

repositories {
    maven {
        name "mikroskeem"
        url "https://repo.wut.ee/repository/mikroskeem-repo"
    }
}

dependencies {
    api project(":AuraBan-api")
    api project(":AuraBan-bungeecord")
    api project(":AuraBan-velocity")
    api project(":AuraBan-nukkit")
    api project(":AuraBan-spigot")
    api project(":AuraBan-sponge")

    // Shadow dependencies
    shadow("eu.mikroskeem:picomaven:${project(":AuraBan-common").picoMaven_version}") {
        exclude group: "org.slf4j"
    }

    // SLF4J files
    slf4j "org.slf4j:slf4j-api:${project(":AuraBan-common").slf4j_version}"
    slf4j "team.aura_dev.lib.slf4j-plugin.bungeecord:slf4j-plugin-bungeecord:${project(":AuraBan-common").slf4jPlugin_version}:${project(":AuraBan-common").slf4j_version}"
    slf4j "team.aura_dev.lib.slf4j-plugin.spigot:slf4j-plugin-spigot:${project(":AuraBan-common").slf4jPlugin_version}:${project(":AuraBan-common").slf4j_version}"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        exclude "lang"
        exclude "assets/*/lang"
        exclude "*.conf"
        exclude pngPatterns
        exclude "**/*.ogg"

        expand \
            "id": rootProject.pluginId,
            "name": rootProject.pluginName,
            "version": rootProject.version,
            "group": rootProject.group,
            "description": rootProject.description,
            "build": rootProject.buildNum,
            "buildType": rootProject.buildType,
            "minecraftVersion": rootProject.minecraft_version
    }
}

processSources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "slf4jVersion", project(":AuraBan-common").slf4j_version
    inputs.property "slf4jPluginVersion", project(":AuraBan-common").slf4jPlugin_version

    filter (ReplaceTokens, tokens: [
        "slf4jVersion": project(":AuraBan-common").slf4j_version,
        "slf4jPluginVersion": project(":AuraBan-common").slf4jPlugin_version
    ])
}

shadowJar {
    relocate("eu.mikroskeem", "${rootProject.group}.shadow.eu.mikroskeem")
    relocate("licenses", "${rootProject.group}.shadow.licenses")
    relocate("org.apache", "${rootProject.group}.shadow.org.apache")
    relocate("org.codehaus", "${rootProject.group}.shadow.org.codehaus")

    exclude "module-info.class"

    from(project.configurations.slf4j.files) {
        into "org/slf4j"
        rename '(.*).jar', '$1.zip'
    }
}
