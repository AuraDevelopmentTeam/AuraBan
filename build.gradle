apply from: "GradleCommon/common.gradle"

import org.apache.tools.ant.filters.ReplaceTokens

configurations {
    slf4j {
        transitive = false
    }
}

repositories {
    maven {
        name "mikroskeem"
        url "https://repo.wut.ee/repository/mikroskeem-repo"
    }
}

dependencies {
    api project(":AuraBan-bungeecord")
    api project(":AuraBan-velocity")
    api project(":AuraBan-nukkit")
    api project(":AuraBan-spigot")
    api project(":AuraBan-sponge")
    
    shadow(project(":AuraBan-api")) {
        transitive = false
    }
    shadow(project(":AuraBan-common")) {
        transitive = false
    }
    shadow(project(":AuraBan-bungeecord")) {
        transitive = false
    }
    shadow(project(":AuraBan-velocity")) {
        transitive = false
    }
    shadow(project(":AuraBan-nukkit")) {
        transitive = false
    }
    shadow(project(":AuraBan-spigot")) {
        transitive = false
    }
    shadow(project(":AuraBan-sponge")) {
        transitive = false
    }

    // Shadow dependencies
    shadow("eu.mikroskeem:picomaven:${project(":AuraBan-common").picoMaven_version}") {
        exclude group: "org.slf4j"
    }

    // SLF4J files
    slf4j "org.slf4j:slf4j-api:${project(":AuraBan-common").slf4j_version}"
    slf4j "team.aura_dev.lib.slf4j-plugin.bungeecord:slf4j-plugin-bungeecord:${project(":AuraBan-common").slf4jPlugin_version}:${project(":AuraBan-common").slf4j_version}"
    slf4j "team.aura_dev.lib.slf4j-plugin.nukkit:slf4j-plugin-nukkit:${project(":AuraBan-common").slf4jPlugin_version}:${project(":AuraBan-common").slf4j_version}"
    slf4j "team.aura_dev.lib.slf4j-plugin.spigot:slf4j-plugin-spigot:${project(":AuraBan-common").slf4jPlugin_version}:${project(":AuraBan-common").slf4j_version}"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        exclude "lang"
        exclude "assets/*/lang"
        exclude "*.conf"
        exclude pngPatterns
        exclude "**/*.ogg"

        expand \
            "id": rootProject.pluginId,
            "name": rootProject.pluginName,
            "version": rootProject.version,
            "group": rootProject.group,
            "description": rootProject.description,
            "build": rootProject.buildNum,
            "buildType": rootProject.buildType,
            "minecraftVersion": rootProject.minecraft_version
    }
}

processSources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "slf4jVersion", project(":AuraBan-common").slf4j_version
    inputs.property "slf4jPluginVersion", project(":AuraBan-common").slf4jPlugin_version

    filter (ReplaceTokens, tokens: [
        "slf4jVersion": project(":AuraBan-common").slf4j_version,
        "slf4jPluginVersion": project(":AuraBan-common").slf4jPlugin_version
    ])
}

shadowJar {
    relocate("eu.mikroskeem", "${rootProject.group}.shadow.eu.mikroskeem")
    relocate("licenses", "${rootProject.group}.shadow.licenses")
    relocate("org.apache", "${rootProject.group}.shadow.org.apache")
    relocate("org.codehaus", "${rootProject.group}.shadow.org.codehaus")

    exclude "module-info.class"

    from(project.configurations.slf4j.files) {
        into "org/slf4j"
        rename '(.*).jar', '$1.zip'
    }
}

delombok {
    onlyIf {
        false
    }
}
